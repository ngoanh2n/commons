/**
 * @author Ho Huu Ngoan (ngoanh2n@gmail.com)
 * @since 2021-01-16
 * Allure Gradle Plugin: https://github.com/allure-framework/allure-gradle
 */
buildscript {
    apply from: "gradle/versions.gradle"
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {}
}

plugins {
    id("java")
    id("idea")
    id("java-library")
    id("io.qameta.allure") version "2.11.2"
}

group(property("prj.groupId"))
version(property("prj.version"))
description(property("prj.desc") as String)

apply from: "gradle/testing.gradle"
apply from: "gradle/compiling.gradle"
apply from: "gradle/publishing.gradle"
apply from: "gradle/dependencies.gradle"

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

reporting {
    setBaseDir(layout.buildDirectory.dir("reports"))
}

allure {
    version.set("${VERSION_ALLURE}")
    adapter {
        aspectjWeaver.set(true)
        aspectjVersion.set("1.9.5")
        autoconfigureListeners.set(true)

        frameworks {
            junit5 {
                enabled.set(true)
                autoconfigureListeners.set(true)
            }
        }
    }
    report {
        reportDir.set(project.reporting.baseDirectory.dir("allure-report"))
    }
}

clean.doFirst {
    delete project.file("out")
}
